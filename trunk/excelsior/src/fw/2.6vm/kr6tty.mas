% Init E-stack
        [ CALL ADR=INIT_STACK ]
% Init memory
        [ CONST->R1 D=128. ]
        [ CONST->R2 D=0 ALU=NE(B) ]
:INIT3  [ WR_MEM_A bus_A=RW_R1 ALU=A-1 FLAG=Z ]
        [ WR_MEM_D bus_B=R2 GO_FALSE ADR=INIT3 ]
%
        [ CONST->R1 D=3 SHIFT_EN ]
        [ bus_B=RW_R1   SHIFT_EN ]
:INIT2  [ WR_MEM_A bus_A=RW_R1 ALU=A-1 FLAG=Z ]
        [ WR_MEM_D bus_B=R2 GO_FALSE ADR=INIT2 ]
% Reset Q-bus
        [ CALL ADR=RESET_SUB ]
% Create tables
% 0
        [ CONST->R1 D=1 SHIFT_EN ]
        [ CONST D=0 WR_MEM_A ] [ bus_B=R1  WR_MEM_D ]
        [ CONST D=1 WR_MEM_A ] [ bus_B=R1  WR_MEM_D ]
        [ CONST D=2 WR_MEM_A ] [ CONST D=4 WR_MEM_D ]
        [ CONST D=3 WR_MEM_A ] [ CONST D=0 WR_MEM_D ]
% 1
        [ CONST->R2 SHIFT_EN D=2 ]
        [ ALU=A bus_A=R1 bus_B=WR_R3 ] % R3 = 100h
        [ bus_B=R1  WR_MEM_A ]
        [ bus_B=R2  WR_MEM_D ]
        [ ALU=A+B bus_A=RW_R2 bus_B=R3 ]
        [ ALU=A+1 bus_A=RW_R1 WR_MEM_A ]
        [ bus_B=R2 WR_MEM_D ]
        [ ALU=A+B bus_A=RW_R2 bus_B=R3 ]
        [ ALU=A+1 bus_A=RW_R1 WR_MEM_A ]
        [ bus_B=R2 WR_MEM_D ]
        [ ALU=A+B bus_A=RW_R2 bus_B=R3 ]
        [ ALU=A+1 bus_A=RW_R1 WR_MEM_A ]
        [ bus_B=R2 WR_MEM_D ]
        [ ALU=ROR(B) CONST->R4 D=1 ]
        [ CONST->R5 D=0FC ]
:TBL1   [ ALU=A-1 bus_A=RW_R5 FLAG=Z ]
        [ ALU=A+1 bus_A=RW_R1 WR_MEM_A ]
        [ bus_B=R4 WR_MEM_D GO_FALSE ADR=TBL1 ]
% 2,3
        [ CONST->R5 D=2 SHIFT_EN ]
        [ CONST->R2 D=10 SHIFT_EN ]
:TBL3   [ ALU=A+1 bus_A=RW_R1 WR_MEM_A ]
        [ bus_B=R2 WR_MEM_D ]
        [ ALU=A+B bus_A=RW_R2 bus_B=R3 ]
        [ ALU=A-1 bus_A=RW_R5 FLAG=Z ]
        [ GO_FALSE ADR=TBL3 ]
% 4,5
        [ CONST->R5 D=2 SHIFT_EN ]
        [ CONST->R2 D=8 SHIFT_EN ]
        [ bus_B=RW_R2   SHIFT_EN ]
:TBL4   [ ALU=A+1 bus_A=RW_R1 WR_MEM_A ]
        [ bus_B=R2 WR_MEM_D ]
        [ ALU=A-1 bus_A=RW_R5 FLAG=Z ]
        [ GO_FALSE ADR=TBL4 ]
% 6
        [ ALU=ROR(B) CONST->R2 D=1 ]
        [ CONST->R5 D=1 SHIFT_EN ]
:TBL5   [ ALU=A+1 bus_A=RW_R1 WR_MEM_A ]
        [ bus_B=R2 WR_MEM_D ]
        [ ALU=A-1 bus_A=RW_R5 FLAG=Z ]
        [ GO_FALSE ADR=TBL5 ]
        [ CONST->R2 D=0 ]
        [ ALU=A-B bus_A=RW_R1 bus_B=R3 ]
        [ CONST->R5 D=7 ]
:TBL6   [ ALU=A+1 bus_A=RW_R1 WR_MEM_A ]
        [ bus_B=R2 WR_MEM_D ]
        [ ALU=A+B bus_A=RW_R2 bus_B=R3 ]
        [ ALU=A-1 bus_A=RW_R5 FLAG=Z ]
        [ GO_FALSE ADR=TBL6 ]
% 7  Q-bus
        [ CONST->R2 D=7 SHIFT_EN ]
        [ CONST->R1 D=1 SHIFT_EN ]
        [ ALU=A+B bus_A=R1 CONST D=40 WR_MEM_A ]
        [ bus_B=R2 WR_MEM_D ]
        [ CONST->R4 D=40 SHIFT_EN ]
        [ bus_B=RW_R4 SHIFT_EN ]
        [ CONST->R5 D=1 SHIFT_EN ]
:TLB7   [ bus_B=R2 WR_MEM_A ]
        [ bus_B=R4 WR_MEM_D ]
        [ ALU=A-1 bus_A=RW_R5 FLAG=Z ]
        [ ALU=A+B bus_A=RW_R4 bus_B=R1 ]
        [ ALU=A+1 bus_A=RW_R2 GO_FALSE ADR=TLB7 ]
%  NIL
        [ CONST->R2 D=4 SHIFT_EN ]
        [ CONST->R1 D=1 SHIFT_EN ]
        [ ALU=A+B bus_A=R1 CONST D=0FF WR_MEM_A ]
        [ bus_B=R2 WR_MEM_D ]

% Reset interupt requests.
:INIT4  [ CONST->M D=0            ]
        [ bus_B=M RESET_ACL       ]
        [ bus_B=M RESET_TIMER     ]
        [ bus_B=M RESET_DEBUG     ]
        [ bus_B=M RESET_HARD_ERR  ]
% Create process
        [ CONST->P D=4 SHIFT_EN   ]
        [ CONST->F D=5 SHIFT_EN   ]
        [ ALU=A bus_A=F bus_B=WR_PC ]
        [ ALU=A RW_CASH_PC ]
        [ CONST D=0 WR_CASH ]
        [ ALU=A+B bus_A=P CONST->G D=10 ]
        [ ALU=A+B bus_A=P CONST->L D=20 ]
        [ ALU=A+B bus_A=P CONST->S D=24 ]
        [ ALU=A+B bus_A=P CONST->H D=0FF ]
        [ ALU=A bus_A=G bus_B=WR_R1 ]
        [ ALU=A bus_A=F bus_B=WR_R4 CALL ADR=WRITE ]
        [ ALU=A+B bus_A=P CONST->R1 D=5 ]
        [ ALU=A bus_A=H bus_B=WR_R4 CALL ADR=WRITE ]
        [ CALL ADR=SAVE_REGS ]
        [ CONST->R1 D=8 ]

:CONSOL
        [ CONST->Time D=2 ]
        [ bus_A=R1->STACK CALL ADR=CRLF ]
        [ CONST->R1 D=48 CALL_REG ]
        [ CONST->R1 D=41 CALL_REG ]
        [ CONST->R1 D=4C CALL_REG ]
        [ CONST->R1 D=54 CALL_REG ]
        [ CONST->R1 D=20 CALL_REG ]
        [ bus_A=STACK->R1 ]
        [ CONST->R2 D=2 ]
        [ CALL ADR=PUT_NUM ]
:CONSOL9[ ALU=A bus_A=P bus_B=WR_R3 ]
:CONSOL1[ CALL ADR=CRLF ]

:CONSOL_LOOP
% R3 - address, R4 - (R3), R5 - number, R6 - digits counter.
% Time - mode
        [ CONST->R1 D=20 CALL_REG ]
:MEM_CONSOL_CR
        [ ALU=B bus_A=WR_R5 CONST->R6 D=0 ]
        [ ALU=A-B bus_A=Time CONST D=1 FLAG=Z ]
        [ GO_TRUE ADR=IO_CONSOL ]
        [ ALU=A-B bus_A=Time CONST D=2 FLAG=Z ]
        [ GO_TRUE ADR=VM_CONSOL ]
        [ CONST->R2 D=8 ]
        [ ALU=A bus_A=R3 bus_B=WR_R1 CALL ADR=PUT_NUM ]
        [ CONST->R2 D=8 ]
        [ bus_B=R3 RD_MEM_A ]
        [ bus_B=WR_R4 RD_MEM_D GOTO ADR=CONSOL4 ]
:IO_CONSOL
        [ CONST->R1 D=49 CALL_REG ]
        [ CONST->R1 D=4F CALL_REG ]
        [ CONST->R1 D=20 CALL_REG ]
        [ CONST->R2 D=3 ]
        [ ALU=A bus_A=R3 bus_B=WR_R1 CALL ADR=PUT_NUM ]
        [ ALU=A bus_A=R3 bus_B=WR_Etop CALL ADR=GET_IO_ADR ]
        [ CONST->R2 D=4 ]
        [ bus_B=R1 RD_MEM_A ]
        [ bus_B=WR_R4 RD_MEM_D GOTO ADR=CONSOL4 ]

:VM_CONSOL
        [ CONST->R1 D=56 CALL_REG ]
        [ CONST->R1 D=4D CALL_REG ]
        [ CONST->R1 D=20 CALL_REG ]
        [ CONST->R2 D=6 ]
        [ ALU=A bus_A=R3 bus_B=WR_R1 CALL ADR=PUT_NUM ]
        [ bus_A=R3->STACK ALU=A bus_B=WR_R1 CALL ADR=CHECK ]
        [ bus_A=STACK->R3 GO_FALSE ADR=VM_NO_PAGE ]
        [ bus_A=R3->STACK ALU=A bus_B=WR_R1 CALL ADR=READ ]
        [ ALU=A bus_A=R2 bus_B=WR_R4 ]
        [ CONST->R2 D=8 ]
        [ bus_A=STACK->R3 GOTO ADR=CONSOL4 ]

:VM_NO_PAGE
        [ CONST->R1 D=20 CALL_REG ]
        [ CONST->R1 D=20 CALL_REG ]
        [ CONST->R1 D=20 CALL_REG ]
        [ CONST->R1 D=20 CALL_REG ]
        [ CONST->R1 D=20 CALL_REG ]
        [ CONST->R1 D=20 CALL_REG ]
        [ CONST->R1 D=20 CALL_REG ]
        [ CONST->R1 D=20 CALL_REG ]
        [ GOTO ADR=CONSO10 ]

:CONSOL4[ CONST->R1 D=3D CALL_REG ]
        [ ALU=A bus_A=R4 bus_B=WR_R1 CALL ADR=PUT_NUM ]
:CONSO10[ CONST->R1 D=20 CALL_REG ]
:CONSOL2[ CALL ADR=GET_CH ]
:CONSOL8[ ALU=A-B bus_A=R1 CONST D=0D FLAG=Z ]
        [ GO_TRUE ADR=CR_KEY ]
        [ ALU=A-B bus_A=R1 CONST D=0A FLAG=Z ]
        [ GO_TRUE ADR=LF_KEY ]
        [ ALU=A-B bus_A=R1 CONST D=2F FLAG=Z ]
        [ GO_TRUE ADR=SLASH_KEY ]
        [ ALU=A-B bus_A=R1 CONST D=4D FLAG=Z ]
        [ GO_TRUE ADR=MEM_KEY ]
        [ ALU=A-B bus_A=R1 CONST D=49 FLAG=Z ]
        [ GO_TRUE ADR=IO_KEY ]
        [ ALU=A-B bus_A=R1 CONST D=56 FLAG=Z ]
        [ GO_TRUE ADR=VM_KEY ]
        [ ALU=A-B bus_A=R1 CONST D=4C FLAG=Z ]
        [ GO_TRUE ADR=BOOTER ]
        [ ALU=A-B bus_A=R1 CONST D=30 FLAG=CARRY ]
        [ GO_FALSE ADR=CONSOL3 ]
        [ ALU=A-B bus_A=R1 CONST D=3A FLAG=CARRY ]
        [ GO_FALSE ADR=DIGIT_KEY ]
        [ ALU=A-B bus_A=R1 CONST D=41 FLAG=CARRY ]
        [ GO_FALSE ADR=CONSOL3 ]
        [ ALU=A-B bus_A=R1 CONST D=47 FLAG=CARRY ]
        [ GO_TRUE ADR=CONSOL3 ]
        [ ALU=A-B bus_A=RW_R1 CONST D=7 ]
        [ GOTO ADR=DIGIT_KEY ]
:CONSOL3[ ALU=A-B bus_A=R1 CONST D=47 FLAG=Z ]
        [ GO_TRUE ADR=GO_KEY ]
        [ CONST->R1 D=3F CALL_REG ]
        [ GOTO ADR=CONSOL1 ]

:WR_MEMORY
        [ ALU=A bus_A=R6 FLAG=Z ]
        [ RETURN_TRUE ]
        [ ALU=A bus_A=Time FLAG=Z ]
        [ GO_TRUE ADR=SET_MEM ]
        [ ALU=A-1 bus_A=Time FLAG=Z ]
        [ GO_TRUE ADR=SET_IO  ]
        [ bus_A=R3->STACK ALU=A bus_B=WR_R1 CALL ADR=CHECK ]
        [ bus_A=STACK->R3 RETURN_FALSE ]
        [ ALU=A bus_A=R5 bus_B=WR_R4 ]
        [ bus_A=R3->STACK ALU=A bus_B=WR_R1 CALL ADR=WRITE ]
        [ ALU=A bus_A=STACK->R3 RETURN ]
:SET_IO [ ALU=A bus_A=R3 bus_B=WR_Etop CALL ADR=GET_IO_ADR ]
        [ bus_B=R1 WR_MEM_A ]
        [ bus_B=R5 WR_MEM_D RETURN ]
:SET_MEM[ bus_B=R3 WR_MEM_A ]
        [ bus_B=R5 WR_MEM_D RETURN ]

:CR_KEY [ CALL ADR=WR_MEMORY ]
        [ CONST->R1 D=0A CALL_REG ]
        [ CONST->R1 D=2D CALL_REG ]
        [ ALU=A-1 bus_A=RW_R3 GOTO ADR=MEM_CONSOL_CR ]

:LF_KEY [ CALL ADR=WR_MEMORY ]
        [ CONST->R1 D=0D CALL_REG ]
        [ ALU=A+1 bus_A=RW_R3 GOTO ADR=CONSOL_LOOP ]

:SLASH_KEY
        [ ALU=A bus_A=R6 FLAG=Z ]
        [ GO_TRUE ADR=SLASH1 ]
        [ ALU=A bus_A=R5 bus_B=WR_R4 ]
:SLASH1 [ ALU=A bus_A=R4 bus_B=WR_R3 CALL ADR=CRLF ]
        [ GOTO ADR=CONSOL1 ]

:DIGIT_KEY
        [ ALU=A-B bus_A=RW_R1 CONST D=30 ]
        [ ALU=A+1 bus_A=RW_R6 ]
        [ ALU=A+B bus_A=RW_R5 bus_B=R5 ]
        [ ALU=A+B bus_A=RW_R5 bus_B=R5 ]
        [ ALU=A+B bus_A=RW_R5 bus_B=R5 ]
        [ ALU=A+B bus_A=RW_R5 bus_B=R5 ]
        [ ALU=A+B bus_A=RW_R5 bus_B=R1 GOTO ADR=CONSOL2 ]

:MEM_KEY
        [ CONST->Time D=0 ]
        [ CONST->R3 D=0 ]
:MEM_K1 [ CALL ADR=CRLF ]
        [ GOTO ADR=CONSOL_LOOP ]

:IO_KEY
        [ CONST->Time D=1 ]
        [ CONST->R3 D=0 ]
        [ GOTO ADR=MEM_K1 ]

:VM_KEY
        [ CONST->Time D=2 ]
        [ CONST->R3 D=1 ]
        [ GOTO ADR=MEM_K1 ]

:CRLF   [ bus_A=R1->STACK CONST->R1 D=0D ]
        [ CALL ADR=PUT_CH REG_EN ]
        [ CONST->R1 D=0A CALL_REG ]
        [ bus_A=STACK->R1 RETURN ]

:PUT_NUM
        [ bus_A=R3->STACK ]
        [ ALU=B-A bus_A=R2 CONST->R3 D=9 ]
:PUT_NM1[ ALU=A-1 bus_A=RW_R3 FLAG=Z ]
        [ ALU=ROL(A) bus_A=RW_R1 ]
        [ ALU=ROL(A) bus_A=RW_R1 ]
        [ ALU=ROL(A) bus_A=RW_R1 ]
        [ ALU=ROL(A) bus_A=RW_R1 GO_FALSE ADR=PUT_NM1 ]
:PUT_NM3[ ALU=A_AND_B bus_A=R1 CONST->R3 D=0F ]
        [ bus_A=R1->STACK ]
        [ ALU=A+B bus_A=R3 CONST->R1 D=30 ]
        [ ALU=A-B bus_A=R1 CONST D=3A FLAG=CARRY ]
        [ GO_FALSE ADR=PUT_NM2 ]
        [ ALU=A+B bus_A=RW_R1 CONST D=7 ]
:PUT_NM2[ CALL ADR=PUT_CH REG_EN ]
        [ bus_A=STACK->R1 ]
        [ ALU=A-1 bus_A=RW_R2 FLAG=Z ]
        [ ALU=ROL(A) bus_A=RW_R1 ]
        [ ALU=ROL(A) bus_A=RW_R1 ]
        [ ALU=ROL(A) bus_A=RW_R1 ]
        [ ALU=ROL(A) bus_A=RW_R1 GO_FALSE ADR=PUT_NM3 ]
        [ bus_A=STACK->R3 RETURN ]

:GET_CH [ bus_A=R2->STACK ]
:GET_CH1[ CALL ADR=REFRESH ]
        [ bus_B=M QUIT_ON ]
        [ CONST->R2 D=20 SHIFT_EN ]
        [ bus_B=WR_R1 RD_CONSOL ]
        [ ALU=A_AND_B bus_A=R2 bus_B=R1 FLAG=Z ]
        [ GO_TRUE ADR=GET_CH1 ]
:GET_CH3[ ALU=A_AND_B bus_A=RW_R1 CONST D=7F ]
        [ bus_A=STACK->R2 GOTO ADR=PUT_CH ]

:CHECK_HALT
        [ CONST->R2 D=20 SHIFT_EN ]
        [ bus_B=WR_R1 RD_CONSOL ]
        [ ALU=A_AND_B bus_A=R2 bus_B=R1 FLAG=Z ]
        [ RETURN_TRUE
          ALU=A_AND_B bus_A=RW_R1 CONST D=7F ]
        [ ALU=A-B bus_A=R1 CONST D=3 FLAG=Z ]
        [ RETURN_FALSE ]
        [ CONST->R6 D=0 ]
        [ GOTO ADR=HALTED ]

:PUT_CH [ bus_A=R2->STACK ]
        [ bus_A=R3->STACK ]
:PUT_CH1[ CALL ADR=REFRESH ]
        [ bus_B=M QUIT_ON ]
        [ CONST->R2 D=10 SHIFT_EN ]
        [ bus_B=WR_R3 RD_CONSOL ]
        [ ALU=A_AND_B bus_A=R2 bus_B=R3 FLAG=Z ]
        [ GO_TRUE ADR=PUT_CH1 ]
        [ bus_B=R1 WR_CONSOL ]
        [ bus_A=STACK->R3 ]
        [ bus_A=STACK->R2 ]
        [ RETURN ]

:REFRESH
        [ bus_A=R2->STACK FLAG=REF256 ]
        [ CONST->R2 D=80 ]
        [ GO_FALSE ADR=REF_1 ]
        [ ALU=ROL(A) bus_A=RW_R2 ]
:REF_1  [ ALU=A-1 bus_A=RW_R2 FLAG=Z RD_MEM_A ]
        [ RD_MEM_D GO_FALSE ADR=REF_1 ]
        [ bus_B=M RESET_TIMER ]
        [ bus_A=STACK->R2 FLAG=ZERO RETURN ]

:GO_KEY
        [ ALU=A bus_A=R6 FLAG=Z ]
        [ GO_TRUE ADR=GO_KEY1 ]
        [ ALU=A bus_A=R5 bus_B=WR_P GOTO ADR=GO_KEY2 ]
:GO_KEY1[ CONST->R1 D=1 ]
        [ CALL ADR=READ ]
        [ ALU=A bus_A=R2 bus_B=WR_P ]
:GO_KEY2[ CALL ADR=RESTORE_REGS ]
        [ FLAG=ZERO FINISH ]

:BOOTER % R1 = 0 autorun
        [ CONST->Etop D=0F SHIFT_EN ]
        [ ALU=A_OR_B bus_A=RW_Etop CONST D=3D ]
        [ CALL ADR=GET_IO_ADR ]
        [ CONST->Etop D=0F SHIFT_EN ]
        [ ALU=A_OR_B bus_A=RW_Etop CONST D=3C ]
        [ ALU=A bus_A=R1 bus_B=WR_R2 CALL ADR=GET_IO_ADR ]
% R1 - csr, R2 -dtr
        [ CONST->R5 D=1 ]
        [ CONST->R6 D=0 ]
        [ CONST->G  D=10 SHIFT_EN ]
        [ CONST->R3 D=40 SHIFT_EN ]
        [ WR_MEM_A bus_B=R1 ]
        [ WR_MEM_D bus_B=R3 ]

:RD_SECTOR
% R5 - sector, R6 - treak, R1 - CSR, R2 - DTR, G - memory adr
        [ CALL ADR=WAIT_DX ][ WR_MEM_A bus_B=R1 ][ WR_MEM_D CONST D=7 ]
        [ CALL ADR=WAIT_DX ][ WR_MEM_A bus_B=R2 ][ WR_MEM_D bus_B=R5 ]
        [ CALL ADR=WAIT_DX ][ WR_MEM_A bus_B=R2 ][ WR_MEM_D bus_B=R6 ]
        [ CALL ADR=WAIT_DX ][ RD_MEM_A bus_B=R1 ][ RD_MEM_D bus_B=WR_R3 ]
        [ CONST->R4 D=80 SHIFT_EN ]
        [ ALU=A_AND_B bus_A=R4 bus_B=R3 FLAG=Z ]
        [ GO_FALSE ADR=BOOT_ERR ]
        [ bus_A=R5->STACK ]
        [ bus_A=R6->STACK ]
        [ WR_MEM_A bus_B=R1 ][ WR_MEM_D CONST D=3 ]
        [ CONST->L D=32. ]

:RD_BUF [ CALL ADR=WAIT_DX ][ RD_MEM_A bus_B=R2 ][ RD_MEM_D bus_B=WR_R3 ]
        [ ALU=A_AND_B bus_A=RW_R3 CONST D=0FF ]
        [ CALL ADR=WAIT1_DX ][ RD_MEM_A bus_B=R2 ][ RD_MEM_D bus_B=WR_R4 ]
        [ ALU=A_AND_B bus_A=RW_R4 CONST D=0FF ]
        [ CALL ADR=WAIT1_DX ][ RD_MEM_A bus_B=R2 ][ RD_MEM_D bus_B=WR_R5 ]
        [ ALU=A_AND_B bus_A=RW_R5 CONST D=0FF ]
        [ CALL ADR=WAIT1_DX ][ RD_MEM_A bus_B=R2 ][ RD_MEM_D bus_B=WR_R6 ]
        [ ALU=A_AND_B bus_A=RW_R6 CONST D=0FF ]
        [ bus_B=RW_R6 SHIFT_EN ][ ALU=A_OR_B bus_A=RW_R6 bus_B=R5 ]
        [ bus_B=RW_R6 SHIFT_EN ][ ALU=A_OR_B bus_A=RW_R6 bus_B=R4 ]
        [ bus_B=RW_R6 SHIFT_EN ][ ALU=A_OR_B bus_A=RW_R6 bus_B=R3 ]
        [ WR_MEM_A bus_B=G ]
        [ WR_MEM_D bus_B=R6 ]
        [ ALU=A+1 bus_A=RW_G ]
        [ ALU=A-1 bus_A=RW_L FLAG=Z ]
        [ GO_FALSE ADR=RD_BUF ]

        [ bus_A=STACK->R6 ]
        [ bus_A=STACK->R5 ]
        [ ALU=A+1 bus_A=RW_R5 ]
        [ ALU=A-B bus_A=R5 CONST D=27. FLAG=Z ]
        [ GO_FALSE ADR=BOOTER1 ]
        [ ALU=A+1 bus_A=RW_R6 ]
        [ CONST->R5 D=1 ]
:BOOTER1[ CONST->R3 D=14 SHIFT_EN ]
        [ ALU=A-B bus_A=G bus_B=R3 FLAG=CARRY ]
        [ GO_FALSE ADR=RD_SECTOR ]
:BOOTER2[ GOTO ADR=CONSOL9 ]

:BOOT_ERR
        [ CALL ADR=CRLF ]
        [ CONST->R1 D=45 CALL_REG ]
        [ CONST->R1 D=52 CALL_REG ]
        [ CONST->R1 D=52 CALL_REG ]
        [ CONST->R1 D=4F CALL_REG ]
        [ CONST->R1 D=52 CALL_REG ]
        [ GOTO ADR=CONSOL9 ]

:WAIT_DX
        [ CALL ADR=REFRESH ]
:WAIT1_DX
        [ RD_MEM_A bus_B=R1 ]
        [ RD_MEM_D bus_B=WR_Etop ]
        [ ALU=A_AND_B bus_A=Etop CONST D=0F0 FLAG=Z ]
        [ GO_TRUE ADR=WAIT_DX ]
        [ RETURN ]

:INIT_STACK
% Init expression stack
        [ CONST->R1 D=32. ]
:INIT_ST[ ALU=A-1 bus_A=RW_R1 FLAG=Z ]
        [ bus_A=Etop->STACK GO_FALSE ADR=INIT_ST ]
:INIT_ST1
        [ bus_A=STACK->Etop ]
        [ FLAG=STK_EMPTY GO_IF_NOT_REQUEST ADR=INIT_ST1 ]
        [ RETURN ]
